plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
}

application {
    mainClass = 'com.smarthome.SmartHomeServer'
}
tasks.register('runCameraServer', JavaExec) {
    group = "application"
    description = "Run SmartHomeCameraServer"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.smarthome.SmartHomeCameraServer'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

sourceCompatibility = '23'
repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.64.0'
    protobufVersion = '3.25.3'
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    'build/generated/source/proto/main/java',
                    'build/generated/source/proto/main/grpc'
            ]
        }
    }
}

test {
    useJUnitPlatform()
}